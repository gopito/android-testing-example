apply plugin: 'com.android.application'

apply plugin: 'jacoco-android'
apply plugin: 'org.sonarqube'
android {
    compileSdkVersion 28
    testOptions.unitTests.includeAndroidResources = true
    defaultConfig {
        applicationId "ru.android2019.testingexample"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
//        testInstrumentationRunnerArguments clearPackageData: 'true'
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    sourceSets {
        test {
            java.srcDirs = ['src/test/java',
                            'src/test_utils/java']
            resources.srcDirs = ['src/test/resources']
        }

        androidTest {
            java.srcDirs = ['src/androidTest/java']
            assets.srcDirs = ['src/androidTest/assets-androidTest']
        }

    }

    testOptions {
        //Тут в примере от гугла ошибка
//        execution 'ANDROIDX_TEST_ORCHESTRATOR'

//        reportDir "$rootDir/test-reports"
        // Encapsulates options for local unit tests.
        unitTests {
            //For Robolectric
            includeAndroidResources = true

            // Encapsulates options for controlling how Gradle executes local unit tests. For a list
            // of all the options you can specify, read
            all {
                testLogging {
                    events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
                }
                //The maximum number of test processes to start in parallel.
                //Можем запустить 3 тестовых класса папраллельно
                maxParallelForks = 3

                reports.html.enabled = true
                reports.junitXml.enabled = false

                //Чтобы Robolectric не выкачивал из инета джарники при старте тестов
                systemProperty 'robolectric.offline', true
                systemProperty 'robolectric.logging.enabled', true
            }
        }
    }
}

configurations {
    instrumentationApks {
        transitive = false
    }
}



dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.test.espresso:espresso-idling-resource:3.1.1'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'

    //Эта версия работает с PowerMock
    testImplementation 'org.mockito:mockito-core:2.8.0'
    //Зависимости для PowerMock
    testImplementation 'org.powermock:powermock-core:1.7.3'
    testImplementation 'org.powermock:powermock-module-junit4:1.7.3'
    testImplementation 'org.powermock:powermock-api-mockito2:1.7.3'

    testImplementation 'org.robolectric:robolectric:4.2.1'
    //for Robolectric
    testImplementation 'androidx.test:core:1.1.0'

    testImplementation 'androidx.test:runner:1.1.1'
    testImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    testImplementation "androidx.test:rules:1.1.1"
    testImplementation 'androidx.test.espresso:espresso-intents:3.1.1'

    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation "androidx.test:rules:1.1.1"
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.1.1'

    //For DatePicker
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.1.0'

//    androidTestUtil 'androidx.test:orchestrator:1.1.1'
}



sonarqube {
    androidVariant 'debug'
    properties {
        def libraries = System.env.ANDROID_HOME + "/platforms/android-28/android.jar"
/* + ", build/intermediates/exploded-aar*//**//*classes.jar"*/
        property "sonar.projectVersion", "0.0.1"
//        property "sonar.sources", "src"
        property "sonar.binaries", "build/intermediates/app_classes/debug"
        property "sonar.libraries", libraries
        property "sonar.tests", "src/test/java"
//        property "sonar.java.test.binaries", "build/intermediates/classes/test"
        property "sonar.java.test.libraries", libraries
        property "sonar.jacoco.reportPaths", "build/jacoco/testDebugUnitTest.exec"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.junit.reportPath", "build/test-results/testDebugUnitTest"
//        property "sonar.android.lint.report", "build/reports/lint-results-debug.xml"
        property "sonar.projectKey", "testing_example"
        property "sonar.projectName", "Calculator"
    }
}
